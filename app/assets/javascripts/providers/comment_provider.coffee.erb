#
#  Service for manipulating scope data
#
@application.factory 'CommentProvider', [
	'$rootScope'
	'Comment'
	'Helpers'
	'notify'
	'Upload'
	($rootScope, Comment, Helpers, notify, Upload) ->
		#
		#  Comments holder
		#
		comment = {}

		#
		#  Create comment in DB
		#  
		#  params:
		#    scopeTask: <object>
		#      - object what have a comment
		#    comment: <object>
		#      - comment object what will be remove
		#  
		#  return: null
		#  
		comment.create = (scopeTask, comment) ->
			#
			#  success collback
			#
			success = (response) ->
				notify.showModelMessage "<%= lc('activerecord.models.comment') %>", 
					"<%= lc('actions.create') %>",
					response.name
				#
				#  Add comment to scope
				#
				scopeTask.comments.push response.data
				return

			#
			#  error collback
			#
			error = (response) ->
				notify.showObjectArray response.data
				return

			#
			#  Create empty object if user don set enithing
			#
			if comment is undefined
				comment = 
					comment: 
						title: ''
						comment: ''

			#
			#  POST request for create users comment for task
			#
			Upload.upload(
				url: '/api/comments'
				data: 
					task_id: scopeTask.id
					project_id: scopeTask.project_id
					comment: comment
			).then success, error
			return

		#
		#  Remove comment from DB
		#  
		#  params:
		#    scopeTask: <object>
		#      - object what have a comment
		#    comment: <object>
		#      - comment object what will be remove
		#  
		#  return: null
		#  
		comment.remove = (scopeTask, comment) ->
			#
			#  success collback
			#
			success = ->
				notify.showModelMessage "<%= lc('activerecord.models.comment') %>", 
					"<%= lc('actions.delete') %>"
				#
				#  Removing comment from scope
				#
				Helpers.removeRecordByField scopeTask.comments, comment.id

			#
			#  error callback
			#
			error = (error) ->
				notify.show("<%= lc("messages.record.errors.dont_delete")%>")
				return

			#
			#  DELETE request for remove users comment for task
			#
			Comment.remove {
				project_id: scopeTask.project_id
				task_id: scopeTask.id
				id: comment.id
			}, success, error
			return

		#
		#  return comment provider
		#
		return comment
]