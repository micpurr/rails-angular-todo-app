#
#  Tasks controller
#
@application.controller 'TasksController', [ 
	'$scope'
	'$routeParams' 
	'Task'
	'$location'
	'FileUploader'
	'Comment'
	($scope, $routeParams, Task, $location, FileUploader, Comment) ->

		$scope.action = "<%= lc('actions.edit') %>"

		$scope.uploader = new FileUploader {
			url: '/api/comments' 
			headers: 
				'X-CSRF-TOKEN': document.querySelector('meta[name="csrf-token"]').getAttribute('content')
			onBeforeUploadItem: (item)->
				item.formData.push {
					project_id: $scope.task.project_id
					task_id: $scope.task.id
					title: $scope.comment.title
					comment: $scope.comment.comment
				}
			onSuccessItem: (item, response, status, headers) ->
				$scope.task.comments.push(response)
		}

		$scope.comment = {}

		Task.get {project_id: $routeParams.project_id, id: $routeParams.id}, (data) ->
			$scope.task = data
			$scope.task.deadline = new Date($scope.task.deadline)
			if $routeParams.action == 'edit'
				$("#deadline").pickadate('picker').set('select', $scope.task.deadline)

		$scope.formTask = (task) ->
			task.$update {
				id: task.id
				project_id: task.project_id
			}, -> 
				$location.path('/')

		$scope.addComment = () ->
			$scope.uploader.uploadAll()

		$scope.removeComment = (comment) ->
			Comment.remove {
				project_id: $scope.task.project_id
				task_id: $scope.task.id
				id: comment.id
			}, 
				->
					index = $scope.task.comments.indexOf(comment)
					$scope.task.comments.splice(index, 1)

]